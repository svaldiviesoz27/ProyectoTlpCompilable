programa       = { clase }, methodMain ;

clase          = "Class", identificador, [ "extends", identificador ],
                  "{", { miembro }, "}" ;

miembro        = atributo | metodo ;

atributo       = tipo, identificador, [ "=", expresion ], ";" ;

metodo         = "method", identificador,
                  "{", { instruccion }, "}" ;

methodMain     = "methodMain",
                  "{", { instruccion }, "}" ;

tipo           = "int" | "string" | "string[]" | "bool" ;

instruccion    = asignacion | impresion | llamada | comentario ;

asignacion     = identificador, "=", expresion, ";" ;

impresion      = "print", "(", expresion, ")", ";" ;

llamada        = identificador, "(", [ argumentos ], ")", ";" ;

argumentos     = expresion, { ",", expresion } ;

expresion      = termino, { ("+" | "-" | "*"), termino } ;

termino        = numero | cadena | booleano | identificador | lista | "null" ;

lista          = "[", [ cadena, { ",", cadena } ], "]" ;

booleano       = "true" | "false" ;
numero         = digito, { digito } ;
cadena         = '"', { caracter }, '"' ;
comentario     = "//", { caracter } ;

identificador  = letra, { letra | digito | "_" } ;

digito         = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
letra          = "A" | "B" | ... | "Z" | "a" | ... | "z" ;
caracter       = letra | digito | " " | "_" | "." ;
